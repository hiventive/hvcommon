# Block
file(GLOB_RECURSE SOURCE_FILES ${CMAKE_CURRENT_SOURCE_DIR}/*.cpp)

add_library(${CMAKE_PROJECT_NAME_LOWER} ${SOURCE_FILES})
generate_export_header(${CMAKE_PROJECT_NAME_LOWER})
set_target_properties(${CMAKE_PROJECT_NAME_LOWER} PROPERTIES
		VERSION ${${CMAKE_PROJECT_NAME}_VERSION}
		EXPORT_NAME ${CMAKE_PROJECT_NAME_LOWER_WP})

target_link_libraries(${CMAKE_PROJECT_NAME_LOWER} SystemC::systemc SystemC::cci)

# Install

file(GLOB ${CMAKE_PROJECT_NAME_LOWER}_PUBLIC_HEADERS ${CMAKE_CURRENT_SOURCE_DIR}/*.h
		${CMAKE_CURRENT_SOURCE_DIR}/*.hpp)
if (EXISTS ${CMAKE_CURRENT_SOURCE_DIR}/${CMAKE_PROJECT_NAME_LOWER_WP}.h)
	list(REMOVE_ITEM ${CMAKE_PROJECT_NAME_LOWER}_PUBLIC_HEADERS "${CMAKE_CURRENT_SOURCE_DIR}/${CMAKE_PROJECT_NAME_LOWER_WP}.h")
	install(FILES ${CMAKE_CURRENT_SOURCE_DIR}/${CMAKE_PROJECT_NAME_LOWER_WP}.h DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/${HV_LIB_PREFIX})
endif()
set_target_properties(${CMAKE_PROJECT_NAME_LOWER} PROPERTIES
		PUBLIC_HEADER "${${CMAKE_PROJECT_NAME_LOWER}_PUBLIC_HEADERS}")
install(TARGETS ${CMAKE_PROJECT_NAME_LOWER}
		EXPORT ${CMAKE_PROJECT_NAME}Targets
		ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR}
		LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
		PUBLIC_HEADER DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/${HV_LIB_PREFIX}/${CMAKE_PROJECT_NAME_LOWER_WP})

# CMake
set(${CMAKE_PROJECT_NAME}ConfigPackageLocation ${CMAKE_INSTALL_LIBDIR}/cmake/${CMAKE_PROJECT_NAME})
set(${CMAKE_PROJECT_NAME}TargetsFile ${CMAKE_CURRENT_BINARY_DIR}/${CMAKE_PROJECT_NAME}/${CMAKE_PROJECT_NAME}Targets.cmake)
set(${CMAKE_PROJECT_NAME}ConfigFile ${CMAKE_CURRENT_BINARY_DIR}/${CMAKE_PROJECT_NAME}/${CMAKE_PROJECT_NAME}Config.cmake)
set(${CMAKE_PROJECT_NAME}ConfigVersionFile ${CMAKE_CURRENT_BINARY_DIR}/${CMAKE_PROJECT_NAME}/${CMAKE_PROJECT_NAME}ConfigVersion.cmake)
write_basic_package_version_file(${${CMAKE_PROJECT_NAME}ConfigVersionFile}
		VERSION ${${CMAKE_PROJECT_NAME}_VERSION}
		COMPATIBILITY AnyNewerVersion)
export(EXPORT ${CMAKE_PROJECT_NAME}Targets
		FILE ${${CMAKE_PROJECT_NAME}TargetsFile}
		NAMESPACE ${HV_NAMESPACE})
configure_package_config_file(${PROJECT_SOURCE_DIR}/cmake/Config.cmake.in ${${CMAKE_PROJECT_NAME}ConfigFile}
		INSTALL_DESTINATION ${${CMAKE_PROJECT_NAME}ConfigPackageLocation})
install(EXPORT ${CMAKE_PROJECT_NAME}Targets
		FILE ${CMAKE_PROJECT_NAME}Targets.cmake
		NAMESPACE ${HV_NAMESPACE}
		DESTINATION ${${CMAKE_PROJECT_NAME}ConfigPackageLocation})
install(FILES ${${CMAKE_PROJECT_NAME}ConfigFile} ${${CMAKE_PROJECT_NAME}ConfigVersionFile}
		DESTINATION ${${CMAKE_PROJECT_NAME}ConfigPackageLocation})
